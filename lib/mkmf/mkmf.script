#!/sprite/cmds/csh -f
#
# A script to generate (or regenerate) the Makefile for a directory
# consisting solely of a shell script.
#
# We assume we were invoked from mkmf.  Parameters passed in from mkmf
# through environment variables:
#
#	MKMFDIR		directory containing prototype makefiles
#	MAKEFILE	name of makefile to create
#	SUBTYPE		additional information, telling whether this
#			is an X directory, Sprite directory, etc.
#

#
# Argument processing.  (Generalized form, even though just one flag so far.)
#
while ($#argv >= 1)
    if ("$1" == '-x') then
	set echo
    endif
    shift
end

set subtype=$SUBTYPE
set prog=$cwd:t
set machines=`grep -v "^#" /sprite/admin/machineTypes`
set makefile=$MAKEFILE
set distdir=($DISTDIR)

if (-e $makefile.proto) then
	set proto=$makefile.proto
else
	set proto="${MKMFDIR}/Makefile.script"
endif

echo "Generating $makefile for $prog using $proto"

set nonomatch
set manPages = (*.man)
if ("$manPages" == "*.man") then
    set manPages = ()
endif
unset nonomatch

cat $proto | sed \
	-e "s,@(DATE),`date`,g" \
	-e "s,@(MACHINES),$machines,g" \
	-e "s,@(MAKEFILE),$makefile,g" \
	-e "s,@(MANPAGES),$manPages,g" \
	-e "s,@(NAME),$prog,g" \
	-e "s,@(TEMPLATE),$proto,g" \
	-e "s,@(TYPE),$subtype,g" \
	-e "s,@(DISTDIR),$distdir,g" \
	> $makefile
